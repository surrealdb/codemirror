@top SurrealQL {
	expressions
}

@top PermissionInput {
	WhereClause | none | full
}

@top IndexInput {
	IndexClause
}

@top CombinedResults {
	CombinedResult { CommentResult expression CommentResultClose { "" } }+
}

@top Syntax {
	(
		(for !for (create | update | delete | select)) |
		anyKeyword |
		anyLiteral |
		SyntaxDescribe { "@" rawident }
		| "|" | "[" | "]"
	)*
}

@precedence {
	range @left
	method
	binary @left
	union @left
	filter
	for
	clause
}

expressions {
	(expression ";")* expression ";"?
}

expression {
	statement |
	value
}

SubQuery {
	"("
	expression
	")"
}

Block {
	"{" expressions? "}"
}

Closure[since=2_0_0] {
	ParamList { "|" commaSep<VariableName ":" safeType>? "|" } ("->" type)? Block
}

subqueryStatement {
	IfElseStatement |
	LetStatement |
	DeleteStatement |
	CreateStatement |
	SelectStatement |
	RelateStatement |
	UpdateStatement |
	RemoveStatement |
	UpsertStatement |
	ReturnStatement |
	AlterStatement |
	DefineStatement |
	RebuildStatement |
	InsertStatement
}

statement[@isGroup=Statement] {
	BeginStatement |
	CancelStatement |
	CommitStatement |
	InfoForStatement |
	KillStatement |
	LiveSelectStatement |
	ShowStatement |
	SleepStatement |
	UseStatement |
	OptionStatement |
	BreakStatement |
	ContinueStatement |
	ForStatement |
	ThrowStatement |
	subqueryStatement
}

// Transaction statements

BeginStatement { begin transaction? }
CancelStatement { cancel transaction? }
CommitStatement { commit transaction? }

// Define statements

DefineStatement {
	define
	(
	AccessDefinition[since=2_0_0] { access defineAccessOptions } |
	( namespace defineNamespaceOptions ) |
	( database defineDatabaseOptions ) |
	( user defineUserOptions ) |
	( token defineTokenOptions ) |
	( event defineEventOptions ) |
	( field defineFieldOptions ) |
	( index defineIndexOptions ) |
	( analyzer defineAnalyzerOptions ) |
	( functionKw defineFunctionOptions ) |
	( param defineParamOptions ) |
	ScopeDefinition[until=2_0_0] { scope defineScopeOptions } |
	( table defineTableOptions ) |
	( config defineConfigOptions ) |
  ( api defineApiOptions )
	)
}

defineAccessOptions {
	(IfNotExistsClause | overwrite)?
	Ident
	accessClauses1 {
		OnRootNsDbClause
		AccessTypeClause
	}
	accessClauses2 {
		AuthenticateClause?
		DurationClause?
		CommentClause?
	}
}

defineAnalyzerOptions {
	(IfNotExistsClause | OverwriteClause)?
	Ident
	(
		TokenizersClause |
		FiltersClause |
		FunctionClause |
		CommentClause
	)*
}

defineEventOptions {
	(IfNotExistsClause | OverwriteClause)?
	Ident
	OnTableClause
	(
		WhenClause {
			when
			value
		} |
		ThenClause {
			then
			commaSep<(SubQuery | Block)>
		} |
		CommentClause
	)*
}

defineDatabaseOptions {
	(IfNotExistsClause | OverwriteClause)?
	Ident on Ident
	CommentClause?
}

defineFieldOptions {
	(IfNotExistsClause | OverwriteClause)?
	Ident
	OnTableClause
	(
		TypeClause |
		DefaultClause |
		ReadonlyClause |
		ValueClause |
		AssertClause |
		PermissionsForClause |
		CommentClause
	)*
}

defineFunctionOptions {
	(IfNotExistsClause | OverwriteClause)?
	customFunctionName
	ParamList { "(" commaSep<VariableName ":" type>? ")" }
	("->" type)?
	Block
	(
		PermissionsBasicClause |
		CommentClause
	)*
}

functionKw[@name=Keyword] { function }

defineIndexOptions {
	(IfNotExistsClause | OverwriteClause)?
	Ident
	OnTableClause
	(
		FieldsColumnsClause |
		IndexClause |
		CommentClause |
		ConcurrentlyClause[since=2_0_0] { concurrently }
	)*
}

defineNamespaceOptions {
	(IfNotExistsClause | OverwriteClause)?
	Ident
	CommentClause?
}

defineParamOptions {
	(IfNotExistsClause | OverwriteClause)?
	VariableName
	valueKeyword
	value
	(
		PermissionsBasicClause |
		CommentClause
	)*
}

defineScopeOptions {
	(IfNotExistsClause | OverwriteClause)?
	Ident
	(
		SessionClause |
		SigninClause |
		SignupClause |
		CommentClause
	)*
}

defineTableOptions {
	(IfNotExistsClause | OverwriteClause)?
	Ident
	(
		drop |
		(schemafull | schemaless) |
		TableTypeClause |
		TableViewClause |
		ChangefeedClause |
		PermissionsForClause |
		CommentClause
	)*
}

defineConfigOptions {
	(IfNotExistsClause | OverwriteClause)?
	(
		graphql defineConfigGraphqlOptions |
		api defineConfigApiOptions
	)
}

defineConfigGraphqlOptions {
	(
		none |
		auto |
		tables ( none | auto | include commaSep<Ident> | exclude commaSep<Ident> ) |
		functions ( none | auto )
	)*
}

defineConfigApiOptions {
	ApiOptions
}

defineTokenOptions {
	(IfNotExistsClause | OverwriteClause)?
	Ident
	( on ( namespace | database | scope Ident ) )
	TokenTypeClause
	( valueKeyword String )
}

defineUserOptions {
	(IfNotExistsClause | OverwriteClause)?
	Ident
	OnRootNsDbClause
	( ( password | passhash ) String )
	( roles commaSep<Ident> )
	DurationClause?
}

defineApiOptions {
	(IfNotExistsClause | OverwriteClause)?
	String
	ApiOptions
	(
		for
		(any | commaSep<HttpMethod>)
		ApiOptions
		then Block
	)+
}

ApiOptions {
	(
		PermissionsBasicClause |
		MiddlewareClause
	)*
}

HttpMethod {
	get | put | post | delete | patch | trace
}

// Alter statement

AlterStatement[since=2_0_0] {
	alter
	(
	( table alterTableOptions )
	)
}

alterTableOptions {
	IfNotExistsClause?
	Ident
	(
		drop |
		(schemafull | schemaless) |
		PermissionsForClause |
		CommentClause
	)*
}

// Remove statement

RemoveStatement {
	remove
	(
	( namespace IfExistsClause? Ident (and expunge)? ) |
	( database IfExistsClause? Ident (and expunge)? ) |
	( user IfExistsClause? Ident on ( root | namespace | database ) ) |
	( token IfExistsClause? Ident on ( namespace | database | scope ) ) |
	( event IfExistsClause? Ident on table? Ident ) |
	( field IfExistsClause? Ident on table? Ident ) |
	( index IfExistsClause? Ident on table? Ident ) |
	( analyzer IfExistsClause? Ident ) |
	( functionKw IfExistsClause? customFunctionName ) |
	( param IfExistsClause? VariableName ) |
	( scope IfExistsClause? Ident ) |
	( table IfExistsClause? Ident (and expunge)? )
	)
}

// Data statements

CreateStatement {
	create only?
	createTargets { commaSep<Ident | VariableName | FunctionCall | RecordId> }
	createContent { ContentClause | SetClause | UnsetClause | () }
	ReturnClause?
	TimeoutClause?
	ParallelClause?
}

SelectStatement {
	(select)
	(Fields)
	(OmitClause?)
	(from)
	(only)?
	(
		statement |
		(
			commaSep<value>
			(
				WithClause |
				WhereClause |
				SplitClause |
				GroupClause |
				OrderClause |
				LimitStartComboClause |
				FetchClause |
				TimeoutClause |
				ParallelClause |
				TempfilesClause |
				ExplainClause |
				VersionClause
			)*
		)
	)
}

DeleteStatement {
	delete only?
	(
		statement |
		(
			commaSep<value>
			WhereClause?
			ReturnClause?
			TimeoutClause?
			ParallelClause?
		)
	)
}

InsertStatement {
	insert
	ignore?
	relation?
	(into Ident)?
	(
		// TODO this should be value, but cannot easily union with VALUES syntax
		Object |
		VariableName |
		BulkInsert[since=2_0_0] { ( "[" commaSep<Object> "]" ) } |
		(
			"(" commaSep<Ident> ")"
			values
			commaSep<(
				"(" commaSep<value> ")"
			)>
		)
	)
	(
		on duplicate key update
		commaSep<FieldAssignment>
	)?
	InsertReturnClause[since=2_0_0] { ReturnClause? }
}

IfElseStatement {
	if
	(
		Legacy {
			value
			then
			( Block | SubQuery)
			(
				else if
				value
				then
				( Block | SubQuery)
			)*
			(
				else
				( Block | SubQuery)
			)?
			end
		} |
		Modern {
			value Block
			(
				else if
				value Block
			)*
			(
				else Block
			)?
		}
	)
}

relateSubject { Array | Ident | FunctionCall | VariableName | RecordId }
RelateStatement {
	relate only?
	relateSubject ("->" | "<-") relateSubject ("->" | "<-") relateSubject
	( ContentClause | SetClause )?
	ReturnClause?
	TimeoutClause?
	ParallelClause?
}

InfoForStatement {
	info for
	(
		root |
		ns | namespace |
		db | database |
		( sc Ident | scope Ident) |
		( tb Ident | table Ident)
	)
	structure?
}

UpdateStatement {
	update only?
	(
		statement |
		(
			commaSep<value>
			updateClause1 {
				ContentClause |
				ReplaceClause |
				MergeClause |
				PatchClause |
				SetClause |
				UnsetClause
			}?
			updateClause2 {
				WhereClause?
				ReturnClause?
				TimeoutClause?
				ParallelClause?
			}
		)
	)
}

UpsertStatement[since=2_0_0] {
	upsert only?
	(
		statement |
		(
			commaSep<value>
			updateClause1 {
				ContentClause |
				ReplaceClause |
				MergeClause |
				PatchClause |
				SetClause |
				UnsetClause
			}?
			updateClause2 {
				WhereClause?
				ReturnClause?
				TimeoutClause?
				ParallelClause?
			}
		)
	)
}

// Control statements
BreakStatement { break }
ContinueStatement { continue }
ForStatement { for VariableName in ( Array | VariableName | SubQuery | Block ) Block }
ReturnStatement { return expression }
ThrowStatement { throw value }

// Live statements
KillStatement { kill String }
LiveSelectStatement {
	live select
	(
		diff |
		valueKeyword Predicate |
		commaSep<inclusivePredicate>
	)
	from commaSep<( Ident | RecordId )>
	WhereClause?
	FetchClause?
}

// Other statements
LetStatement {
	let VariableName TypedLetStatement[since=2_0_0] { (":" type)? } "=" (value | subqueryStatement)
}

ShowStatement {
	show changes for table
	Ident
	( since String )?
	( limit number )?
}

SleepStatement {
	sleep Duration
}

useNs { ( ns | namespace ) Ident }
useDb { ( db | database ) Ident }
UseStatement {
	use
	( useNs | useDb | ( useNs useDb ) )
}

OptionStatement {
option
Ident
( "=" ( true | false ) )?
}

RebuildStatement[since=1_5_0] {
rebuild index
IfExistsClause?
Ident
OnTableClause
}

// Values

baseValue {
	String |
	prefixedString |
	FormatString |
	number |
	true |
	false |
	null |
	none |
	Regex |
	VariableName |
	Ident[@dynamicPrecedence=-1] { rawident } |
	Array |
	RecordId |
	FunctionJs |
	FunctionCall |
	SubQuery |
	Block |
	Closure |
	Object |
	TypeCast |
	Duration |
	Point { "(" number "," number ")" }
}

Any { "?" }
AnyStar[@name=Any] { "*" }
At[since=2_1_0] { "@" }
Optional[since=2_0_0] { "?" }

value[@isGroup=Value] {
	baseValue |
	BinaryExpression |
	Range |
	Path
}

// Binary expressions

BinaryExpression {
	value !binary Operator value
}

Range[since=2_0_0] {
	rangeOpOpenBoth |
	value !range rangeOpOpenRight |
	rangeOpOpenLeft !range value |
	value !range rangeOp !range value
}

Path {
	baseValue pathElement+ |
	At ( dotPart pathElement* | pathElement+ ) |
	GraphPath pathElement*
}

pathElement {
	GraphPath |
	Subscript {
		Optional?
		"."
		dotPart
	} |
	Filter { !filter "[" (WhereClause | WhereClause { "?" value } | expression) "]" }
}

dotPart {
	At |
	Ident |
	IdiomFunction |
	AnyStar |
	Destructure |
	Recurse
}

GraphPath {
	("<-" | "->" | "<->")
	(Ident | Any | GraphSelection)
}

GraphSelection {
	"("
	GraphFieldSelection[since=2_2_0] {
		select
		Fields
		from
	}?
	commaSep<GraphPredicate>
	(
		WhereClause |
		GraphSplitClause[since=2_2_0] { SplitClause } |
		GraphGroupClause[since=2_2_0] { GroupClause } |
		GraphOrderClause[since=2_2_0] { OrderClause } |
		GraphLimitStartComboClause[since=2_2_0] { LimitStartComboClause } |
		as Ident
	)*
	")"
}

GraphPredicate {
	(value | Any)
}

Destructure[since=2_0_0] {
	"{" commaSep<((Ident ":")? ( Ident | GraphPath ) pathElement* )> "}"
}

IdiomFunction[since=2_0_0] { 
	FunctionName { rawident !method } ArgumentList 
}

// Recursion

Recurse[since=2_1_0] {
	"{" RecurseRange RecurseOptions "}"
	( "(" pathElement+ ")" )?
}

RecurseRange {
	number |
	rangeOpOpenBoth |
	number rangeOpOpenRight |
	rangeOpOpenLeft number |
	number rangeOp number
}

RecurseOptions[since=2_2_0] {
  ("+" recurseOptionName ("=" baseValue)?)*
}

recurseOptionName[@name=FunctionName] {
  rawident
}

// Fields

Fields {
	valueKeyword Predicate |
	commaSep<inclusivePredicate>
}

// Objects

ObjectKey {
	KeyName { rawident } |
	String
}

ObjectProperty {
	ObjectKey ":" value
}

ObjectContent {
	commaSepTrailing<ObjectProperty>
}

Object {
	objectOpen ObjectContent? "}"
}

// Arrays

Array { "[" commaSep<value>? "]"}

// Record Ids

RecordTbIdent {
	rawident |
	tickIdent |
	bracketIdent
}

RecordIdIdent {
	numberident |
	tickIdent |
	bracketIdent
}

recordIdValue {
	RecordIdIdent |
	number |
	Array |
	Object |
	RecordIdString { prefixedString }
}

RecordIdRange {
	rangeOpOpenBoth |
	recordIdValue !range rangeOpOpenRight |
	rangeOpOpenLeft !range recordIdValue |
	recordIdValue !range rangeOp !range recordIdValue
}

RecordId {
	RecordTbIdent
	":"
	( recordIdValue | RecordIdRange )
}

// Durations

Duration {
	DurationPart+
}

// JS Functions

FunctionJs {
	FunctionName { function }
	ArgumentList
	JavaScriptBlock
}

@skip {} {
	JavaScriptBlock {
		"{" JavaScriptContent { javaScriptElement* } "}"
	}
	javaScriptElement {
		javaScriptContent | javaScriptBraceOpen javaScriptElement* javaScriptBraceClose
	}
	BlockComment {
		"/*" (blockCommentContent | blockCommentNewline)* blockCommentEnd
	}
	FormatString {
		'f"' (formatStringContentDouble | Interpolation)* '"' |
		"f'" (formatStringContentSingle | Interpolation)* "'"
	}
}

Interpolation {
	"{" expression "}"
}

// Function invocation

FunctionCall {
	( count ArgumentListCount ) |
	(
		(FunctionName | customFunctionName | rand)
		Version { "<" VersionNumber ">" }?
		ArgumentList
	) |
	RecordId ArgumentList
}

ArgumentList { "(" commaSep<value>? ")" }
ArgumentListCount[@name=ArgumentList] { "(" ( commaSep<value>? | statement ) ")" }

// Assignment

FieldAssignment {
	Ident
	assignmentOperator
	value
}

// Clauses

MergeClause {
	merge Object
}

PatchClause {
	patch Array
}

TableTypeClause[since=1_4_0] {
	typeKeyword
	(
		any |
		normal |
		(
			relation
			( ( in | from ) piped<Ident> )?
			( ( out | to ) piped<Ident> )?
			EnforcedClause[since=2_0_0] { enforced? }
		)
	)
}

TableViewClause {
	as
	select
	commaSep<inclusivePredicate>
	from
	commaSep<value>
	WhereClause?
	GroupClause?
}

ChangefeedClause {
	changefeed
	Duration
}

SessionClause {
	session
	Duration
}

SignupClause {
	signup
	( SubQuery | Block )
}

SigninClause {
	signin
	( SubQuery | Block )
}

AuthenticateClause {
	authenticate
	( SubQuery | Block )
}

JwtClause {
	algorithm Ident key Ident | url String
}

OnTableClause {
	on table? Ident
}

OnRootNsDbClause {
	on ( root | namespace | database )
}

AccessTypeClause {
	typeKeyword (
		( jwt JwtClause ) |
		( record (SignupClause | SigninClause)* ( with jwt JwtClause ( with issuer key Ident )? )? )
	)
}

FieldsColumnsClause {
	( fields | columns ) commaSep<Ident>
}

IndexClause {
	UniqueClause |
	SearchAnalyzerClause |
	MtreeClause |
	HnswClause
}

UniqueClause {
	unique
}

SearchAnalyzerClause {
	search analyzer
	Ident
    (Bm25Clause | DocIdsOrderClause | DocLenghtsOrderClause | PostingsOrderClause |
	 TermsOrderClause | DocIdsCacheClause | DocLenghtsCacheClause | PostingsCacheClause |
	 TermsCacheClause | highlights)*
}

Bm25Clause { bm25 ( "(" number "," number ")")? }
DocIdsCacheClause { doc_ids_cache number }
DocIdsOrderClause { doc_ids_order number }
DocLenghtsCacheClause { doc_lengths_cache number }
DocLenghtsOrderClause { doc_lengths_order number }
PostingsCacheClause { postings_cache number }
PostingsOrderClause { postings_order number }
TermsCacheClause { terms_cache number }
TermsOrderClause { terms_order number }

MtreeClause[since=1_3_0] {
	mtree
	IndexDimensionClause
	(
	MtreeDistClause |
	IndexTypeClause |
	IndexCapacityClause |
	DocIdsOrderClause |
	DocIdsCacheClause |
	MtreeCacheClause
	)*
}

MtreeCacheClause { mtree_cache number }
MtreeDistClause { dist ( Distance ) }

HnswClause[since=1_5_0] {
hnsw
IndexDimensionClause
	(
	HnswDistClause |
	IndexTypeClause |
	IndexCapacityClause |
	IndexLmClause |
	IndexM0Clause |
	IndexMClause |
	IndexEfcClause |
	IndexExtendCandidatesClause |
	IndexKeepPrunedConnectionsClause
	)*
}

HnswDistClause { dist ( Distance | ( minkowski number )) }

IndexDimensionClause { dimension number }
IndexCapacityClause { capacity number }
IndexLmClause { lm number }
IndexM0Clause { m0 number }
IndexMClause { m number }
IndexEfcClause { efc number }
IndexExtendCandidatesClause { extend_candidates }
IndexKeepPrunedConnectionsClause { keep_pruned_connections }

ContentClause {
	content Object
}

ReplaceClause {
	replace Object
}

SetClause {
	set commaSep<FieldAssignment>
}

OmitClause {
    omit commaSep<inclusivePredicate>
}

UnsetClause {
unset commaSep<FieldAssignment>
}

IfNotExistsClause[since=1_3_0] {
	if not exists
}

IfExistsClause[since=1_3_0] {
	if exists
}

OverwriteClause[since=2_0_0] {
	overwrite
}

ReturnClause {
	return
	( before | after | diff | Fields )
}

TimeoutClause {
	timeout
	Duration
}

ParallelClause {
	parallel
}

TempfilesClause[since=2_0_0] {
	tempfiles
}

FetchClause {
	fetch
	commaSep<Path>
}

StartClause {
	start
	at?
	(number | VariableName)
}

LimitClause {
	limit
	by?
	(number | VariableName)
}

LimitStartComboClause {
	StartClause (!clause LimitClause)? |
	LimitClause (!clause StartClause)?
}

GroupClause {
	group
	(
	    by? commaSep<Ident> |
	    all
	)
}

OrderClause {
	order
	by?
	commaSep<Ident>
	( asc | desc )?
}

SplitClause {
	split
	on?
	Ident
}

WhereClause {
	where
	value
}

WithClause {
	with
	(
		noindex |
		index commaSep<Ident>
	)
}

ExplainClause {
	explain
	full?
}

VersionClause[since=2_0_0] {
	version
	prefixedString
}

TokenizersClause { tokenizers commaSep<AnalyzerTokenizer> }

FiltersClause {
	filters
	commaSep<AnalyzerFilters>
}

FunctionClause {
	functionKw
	customFunctionName
}

TypeClause {
	flexible?
	typeKeyword
	type
}

ReadonlyClause[since=1_2_0] {
	readonly
}

DefaultClause {
	default
	DefaultAlways[since=2_2_0] { always? }
	value
}

ValueClause {
	valueKeyword
	value
}

AssertClause {
	assert
	value
}

PermissionsForClause {
	permissions
	(
		none | full |
		(
			for
			commaSep<select | create | update | delete>
			( WhereClause | none | full )
		)+
	)
}

PermissionsBasicClause {
	permissions
	(none | full | WhereClause)
}

MiddlewareClause {
  middleware
  commaSep<FunctionCall>
}

CommentClause {
	comment
	String
}

DurationValue {
	(for token Duration) | (for session Duration)
}

DurationClause[since=2_0_0] {
	duration (for session)? commaSep<DurationValue>
}

TokenTypeClause {
	typeKeyword
	TokenType
}

// Support

AnalyzerFilters {
	Filter ("(" (number "," number | Ident) ")")?
}

Predicate {
	value |
	value as Ident
}

inclusivePredicate {
  AnyStar | Predicate
}

// Types

LiteralType[since=2_0_0] {
	String |
	number |
	Duration |
	ArrayType { "[" commaSep<type> "]" } |
	ObjectType { objectOpen ObjectTypeContent? "}" }
	// TODO support number and duration types
}

singleType {
    TypeName { rawident } |
    ParameterizedType { singleType "<" type ">" } |
    LiteralType
}

type {
    singleType |
    UnionType { type (!union "|" type)+ }
}

safeType {
  singleType | "<" type ">"
}

ObjectTypeContent {
	commaSep<ObjectTypeProperty>
}

ObjectTypeProperty {
	ObjectKey ":" type
}

TypeCast {
	"<" type ">" baseValue
}

Ident {
	rawident |
	tickIdent
}

number { "-"? (Int | Float | Decimal) }

Operator {
    binaryOperator |
	"-" | "=" | ">" | "<" |
	binaryOperatorKeyword |
	is | ( is opNot ) |
	in | ( opNot opIn ) |

	( "@" number "@" ) |
	(
		"<|"
		number
		( "," ( number | Distance | ( minkowski number ) ) )
		"|>"
	)
}

@local tokens {
	blockCommentEnd { "*/" }
	blockCommentNewline { "\n" }
	@else blockCommentContent
}

@external tokens objectToken from "./tokens" {
	objectOpen[@name=BraceOpen]
}

@external tokens rangeOperator from "./tokens" {
	rangeOp[@name=RangeOp],
	rangeOpOpenBoth[@name=RangeOp],
	rangeOpOpenLeft[@name=RangeOp],
	rangeOpOpenRight[@name=RangeOp]
}

@tokens {
	// Characters
	"{"[@name=BraceOpen]
	"}"[@name=BraceClose]
	"[" "]"
	"<" ">"
	":"[@name=Colon]
	"->"[@name=ArrowRight]
	"<-"[@name=ArrowLeft]
	"<->"[@name=ArrowBoth]
	"|"[@name=Pipe]

	// Helpers
	whitespace { $[ \n\r\t] }

	// Comments
	Comment { CommentHash | CommentDash | CommentSlash }
	CommentHash { "#" ![\n]* }
	CommentDash { "--" ![\n]* }
	CommentSlash { "//" ![\n]* }
	CommentResult[@name=Comment,closedBy=CommentResultClose] { "-------- Query" ![\n]* }

	// Numbers
	Int { @digit+ }
	frac { '.' @digit+ }
	exp { $[eE] $[+\-]? @digit+ }
	Float { @digit+ (frac exp? | exp) "f"? }
	Decimal { @digit+ frac? exp? "dec" }
	VersionNumber { @digit+ ("." @digit+ ("." @digit+)?)? }

	String { "'" (!['\\] | "\\" _)* "'" | '"' (!["\\] | "\\" _)* '"' }
	prefixedString[@name=String] { $[ruds] String }

	formatStringContentDouble { ("\\" _ | !["{\\])+ }
	formatStringContentSingle { ("\\" _ | !['{\\])+ }

	// Language
	VariableName { "$" (rawident | tickIdent | bracketIdent) }
	tickIdent {
		"`" ![`]+ '`'
	}
	bracketIdent {
		"⟨" ![⟩]+ "⟩"
	}
	rawident { (@asciiLetter | "_") (@asciiLetter | @digit | "_")* }
	numberident {
		rawident |
		( @digit+ (@asciiLetter | "_") (@asciiLetter | @digit | "_")* )
	}

	functionNameSegment { (@asciiLetter | "_") (@asciiLetter | "_" | @digit)* }
	FunctionName {
		functionNameSegment ("::" functionNameSegment)+
	}
	customFunctionName[@name=FunctionName] {
		"fn" ("::" functionNameSegment)*
	}

	Regex { "/" (![/\\\n[] | "\\" ![\n] | "[" (![\n\\\]] | "\\" ![\n])* "]")+ ("/" $[dgimsuvy]*)? }

	durationUnit { "ns" | "us" | "µs" | "ms" | "s" | "m" | "h" | "d" | "w" | "y"}
	DurationPart { @digit+ " "* durationUnit }

	@precedence { CommentResult Comment Int "-" }
	@precedence { Comment binaryOperator }
	@precedence { prefixedString numberident rawident }
	@precedence { customFunctionName FunctionName rawident }
	@precedence { Float Decimal Int rawident }
	@precedence { DurationPart rawident }
	@precedence { DurationPart Int }
	@precedence { numberident Int rawident }
	@precedence { "f'" 'f"' rawident }
	@precedence { "/*" Regex }
	@precedence { Decimal numberident }
	@precedence { Float numberident }

	javaScriptContent { "\n" | ![{}\n'"]+ "\n"? | String }
	javaScriptBraceOpen { "{" }
	javaScriptBraceClose { "}" }

	binaryOperator {
		"&&" | "||" | "??" | "?:" | "!=" | "==" | "?=" | "*=" | "~" | "!~" | "*~" |
		"<=" | ">=" | "+" | "+=" | "-=" | "*" | "×" | "/"  | "÷" | "**" |
		$[∋∌⊇⊃⊅∈∉⊆⊂⊄] | "@@"
    }

	assignmentOperator[@name=Operator] {
		"="  |
		"+=" |
		"-="
	}
}

commaSep<content> {
	content ("," content)*
}

commaSepTrailing<content> {
	content ("," content)* ","?
}

piped<content> {
	content ("|" content)*
}

@skip { whitespace | Comment | BlockComment }
@detectDelim

@external propSource surqlHighlighting from "./highlight"
@external prop since from "./version"
@external prop until from "./version"

@external extend { rawident } tokens from "./tokens" {
	access					[@name=Keyword],
	algorithm				[@name=Keyword],
	all						[@name=Keyword],
	alter					[@name=Keyword],
	always					[@name=Keyword],
	analyzer				[@name=Keyword],
	and						[@name=Keyword],
	any						[@name=Keyword],
	api						[@name=Keyword],
	as						[@name=Keyword],
	asc						[@name=Keyword],
	assert					[@name=Keyword],
	at						[@name=Keyword],
	authenticate			[@name=Keyword],
	auto					[@name=Keyword],
	begin					[@name=Keyword],
	bm25					[@name=Keyword],
	break					[@name=Keyword],
	by						[@name=Keyword],
	cancel					[@name=Keyword],
	capacity				[@name=Keyword],
	changefeed				[@name=Keyword],
	changes					[@name=Keyword],
	columns					[@name=Keyword],
	comment					[@name=Keyword],
	commit					[@name=Keyword],
	concurrently			[@name=Keyword],
	config					[@name=Keyword],
	content					[@name=Keyword],
	continue				[@name=Keyword],
	create					[@name=Keyword],
	database				[@name=Keyword],
	db						[@name=Keyword],
	default					[@name=Keyword],
	define					[@name=Keyword],
	delete					[@name=Keyword],
	desc					[@name=Keyword],
	dimension				[@name=Keyword],
	dist					[@name=Keyword],
	doc_ids_cache			[@name=Keyword],
	doc_ids_order			[@name=Keyword],
	doc_lengths_cache		[@name=Keyword],
	doc_lengths_order		[@name=Keyword],
	drop					[@name=Keyword],
	duplicate				[@name=Keyword],
	duration				[@name=Keyword],
	efc						[@name=Keyword],
	else					[@name=Keyword],
	end						[@name=Keyword],
	enforced				[@name=Keyword],
	event					[@name=Keyword],
	exclude					[@name=Keyword],
	exists					[@name=Keyword],
	explain					[@name=Keyword],
	expunge					[@name=Keyword],
	extend_candidates		[@name=Keyword],
	fetch					[@name=Keyword],
	field					[@name=Keyword],
	fields					[@name=Keyword],
	filters					[@name=Keyword],
	flexible				[@name=Keyword],
	for						[@name=Keyword],
	from					[@name=Keyword],
	functions				[@name=Keyword],
	get						[@name=Keyword],
	graphql					[@name=Keyword],
	group					[@name=Keyword],
	highlights				[@name=Keyword],
	hnsw					[@name=Keyword],
	if						[@name=Keyword],
	ignore					[@name=Keyword],
	in						[@name=Keyword],
	include					[@name=Keyword],
	index					[@name=Keyword],
	info					[@name=Keyword],
	insert					[@name=Keyword],
	into					[@name=Keyword],
	issuer					[@name=Keyword],
	jwt						[@name=Keyword],
	keep_pruned_connections	[@name=Keyword],
	key						[@name=Keyword],
	kill					[@name=Keyword],
	let						[@name=Keyword],
	limit					[@name=Keyword],
	live					[@name=Keyword],
	lm						[@name=Keyword],
	m						[@name=Keyword],
	m0						[@name=Keyword],
	merge					[@name=Keyword],
	middleware				[@name=Keyword],
	mtree					[@name=Keyword],
	mtree_cache				[@name=Keyword],
	namespace				[@name=Keyword],
	noindex					[@name=Keyword],
	normal					[@name=Keyword],
	not						[@name=Keyword],
	ns						[@name=Keyword],
	omit					[@name=Keyword],
	on						[@name=Keyword],
	only					[@name=Keyword],
	option					[@name=Keyword],
	order					[@name=Keyword],
	out						[@name=Keyword],
	overwrite				[@name=Keyword],
	parallel				[@name=Keyword],
	param					[@name=Keyword],
	passhash				[@name=Keyword],
	password				[@name=Keyword],
	patch					[@name=Keyword],
	permissions				[@name=Keyword],
	post					[@name=Keyword],
	postings_cache			[@name=Keyword],
	postings_order			[@name=Keyword],
	put						[@name=Keyword],
	readonly				[@name=Keyword],
	rebuild					[@name=Keyword],
	record					[@name=Keyword],
	relate					[@name=Keyword],
	relation				[@name=Keyword],
	remove					[@name=Keyword],
	replace					[@name=Keyword],
	return					[@name=Keyword],
	roles					[@name=Keyword],
	root					[@name=Keyword],
	sc						[@name=Keyword],
	schemafull				[@name=Keyword],
	schemaless				[@name=Keyword],
	scope					[@name=Keyword],
	search					[@name=Keyword],
	select					[@name=Keyword],
	session					[@name=Keyword],
	set						[@name=Keyword],
	show					[@name=Keyword],
	signin					[@name=Keyword],
	signup					[@name=Keyword],
	since					[@name=Keyword],
	sleep					[@name=Keyword],
	split					[@name=Keyword],
	start					[@name=Keyword],
	structure				[@name=Keyword],
	table					[@name=Keyword],
	tables					[@name=Keyword],
	tb						[@name=Keyword],
	tempfiles				[@name=Keyword],
	terms_cache				[@name=Keyword],
	terms_order				[@name=Keyword],
	then					[@name=Keyword],
	throw					[@name=Keyword],
	timeout					[@name=Keyword],
	to						[@name=Keyword],
	token					[@name=Keyword],
	tokenizers				[@name=Keyword],
	trace					[@name=Keyword],
	transaction				[@name=Keyword],
	typeKeyword				[@name=Keyword],
	unique					[@name=Keyword],
	unset					[@name=Keyword],
	update					[@name=Keyword],
	upsert					[@name=Keyword],
	url						[@name=Keyword],
	use						[@name=Keyword],
	user					[@name=Keyword],
	valueKeyword			[@name=Keyword],
	values					[@name=Keyword],
	version					[@name=Keyword],
	when					[@name=Keyword],
	where					[@name=Keyword],
	with					[@name=Keyword],

	// Literals
	after					[@name=Literal],
	before					[@name=Literal],
	diff					[@name=Literal],
	false					[@name=Bool],
	full					[@name=Literal],
	none					[@name=None],
	null					[@name=None],
	true					[@name=Bool],

	IndexTypeClause,
	TokenType,

	// Operators
	binaryOperatorKeyword,
	is,
	opIn,
	opNot,

	Distance,
	minkowski				[@name=Distance],
	Filter,

	AnalyzerTokenizer,

	function,
	rand					[@name=FunctionName],
	count					[@name=FunctionName]
}

anyKeyword {
	access |
	algorithm |
	alter |
	analyzer |
	any |
	as |
	asc |
	assert |
	at |
	authenticate |
	auto |
	begin |
	bm25 |
	break |
	by |
	cancel |
	capacity |
	changefeed |
	changes |
	columns |
	comment |
	commit |
	concurrently |
	config |
	content |
	continue |
	create |
	database |
	db |
	default |
	define |
	delete |
	desc |
	dimension |
	dist |
	doc_ids_cache |
	doc_ids_order |
	doc_lengths_cache |
	doc_lengths_order |
	drop |
	duplicate |
	duration |
	efc |
	else |
	end |
	event |
	exclude |
	exists |
	explain |
	extend_candidates |
	fetch |
	field |
	fields |
	filters |
	flexible |
	for |
	from |
	functions |
	graphql |
	group |
	highlights |
	hnsw |
	if |
	ignore |
	in |
	include |
	index |
	info |
	insert |
	into |
	issuer |
	jwt |
	keep_pruned_connections |
	key |
	kill |
	let |
	limit |
	live |
	lm |
	m |
	m0 |
	merge |
	mtree |
	mtree_cache |
	namespace |
	noindex |
	normal |
	not |
	ns |
	on |
	only |
	option |
	order |
	out |
	overwrite |
	parallel |
	param |
	passhash |
	password |
	patch |
	permissions |
	postings_cache |
	postings_order |
	readonly |
	rebuild |
	record |
	relate |
	relation |
	remove |
	return |
	roles |
	root |
	sc |
	schemafull |
	schemaless |
	scope |
	search |
	select |
	session |
	set |
	show |
	signin |
	signup |
	since |
	sleep |
	split |
	start |
	structure |
	table |
	tables |
	tb |
	tempfiles |
	terms_cache |
	terms_order |
	then |
	throw |
	timeout |
	to |
	token |
	tokenizers |
	transaction |
	typeKeyword |
	unique |
	unset |
	update |
	upsert |
	url |
	use |
	user |
	valueKeyword |
	values |
	when |
	where |
	with
}

anyLiteral {
	after |
	before |
	diff |
	false |
	full |
	none |
	null |
	true |

	IndexTypeClause
}
