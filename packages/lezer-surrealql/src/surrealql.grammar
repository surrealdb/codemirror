@top SurrealQL {
	expressions | WhereClause
}

@top PermissionInput {
	WhereClause | none | full
}

@top CombinedResults {
	combinedExpressions
}

@top Syntax {
	(
		(for anyPermission) |
		anyKeyword |
		anyLiteral |
		SyntaxDescribe { "@" rawident }
		| "|" | "[" | "]"
	)*
}

@precedence {
	range @left
	method
	binary @left
	union @left
	filter
}

expressions {
	(expression ";")* expression ";"?
}

combinedExpressions {
	CombinedResult { CommentResult expression CommentResultClose { "" } }+
}

expression {
	statement |
	value
}

SubQuery {
	"("
	expression
	")"
}

Block {
	"{" expressions? "}"
}

Closure {
	ParamList { "|" commaSep<VariableName ":" safeType>? "|" } ("->" type)? Block
}

statement[@isGroup=Statement] {
	BeginStatement |
	CancelStatement |
	CommitStatement |
	DefineStatement |
	AlterStatement |
	RemoveStatement |
	InfoForStatement |
	KillStatement |
	LiveSelectStatement |
	ShowStatement |
	SleepStatement |
	UseStatement |
	OptionStatement |
	RebuildStatement |
	CreateStatement |
	SelectStatement |
	DeleteStatement |
	InsertStatement |
	BreakStatement |
	ContinueStatement |
	ForStatement |
	ReturnStatement |
	ThrowStatement |
	IfElseStatement |
	LetStatement |
	RelateStatement |
	UpdateStatement |
	UpsertStatement
}

// Transaction statements

BeginStatement { begin transaction? }
CancelStatement { cancel transaction? }
CommitStatement { commit transaction? }

// Define statements

DefineStatement {
	define
	(
	( access defineAccessOptions ) |
	( namespace defineNamespaceOptions ) |
	( database defineDatabaseOptions ) |
	( user defineUserOptions ) |
	( token defineTokenOptions ) |
	( event defineEventOptions ) |
	( field defineFieldOptions ) |
	( index defineIndexOptions ) |
	( analyzer defineAnalyzerOptions ) |
	( functionKw defineFunctionOptions ) |
	( param defineParamOptions ) |
	( scope defineScopeOptions ) |
	( table defineTableOptions )
	)
}

defineAccessOptions {
	(IfNotExistsClause | overwrite)?
	Ident
	accessClauses1 {
		OnRootNsDbClause
		AccessTypeClause
	}
	accessClauses2 {
		AuthenticateClause?
		DurationClause?
		CommentClause?
	}
}

defineAnalyzerOptions {
	(IfNotExistsClause | overwrite)?
	Ident
	(
		TokenizersClause |
		FiltersClause |
		FunctionClause |
		CommentClause
	)*
}

defineEventOptions {
	(IfNotExistsClause | overwrite)?
	Ident
	OnTableClause
	WhenThenClause
	CommentClause?
}

defineDatabaseOptions {
	(IfNotExistsClause | overwrite)?
	Ident on Ident
	CommentClause?
}

defineFieldOptions {
	(IfNotExistsClause | overwrite)?
	Ident
	OnTableClause
	(
		TypeClause |
		DefaultClause |
		ReadonlyClause |
		ValueClause |
		AssertClause |
		PermissionsForClause |
		CommentClause
	)*
}

defineFunctionOptions {
	(IfNotExistsClause | overwrite)?
	customFunctionName
	ParamList { "(" commaSep<VariableName ":" type>? ")" }
	("->" type)?
	Block
	(
		PermissionsBasicClause |
		CommentClause
	)*
}

functionKw[@name=Keyword] { function }

defineIndexOptions {
	(IfNotExistsClause | overwrite)?
	Ident
	OnTableClause
	FieldsColumnsClause
	(
		UniqueClause |
		SearchAnalyzerClause |
		MtreeClause |
		HnswClause
	)
}

defineNamespaceOptions {
	(IfNotExistsClause | overwrite)?
	Ident
	CommentClause?
}

defineParamOptions {
	(IfNotExistsClause | overwrite)?
	VariableName
	valueKeyword
	value
}

defineScopeOptions {
	(IfNotExistsClause | overwrite)?
	Ident
	(
		SessionClause |
		SigninClause |
		SignupClause |
		CommentClause
	)*
}

defineTableOptions {
	(IfNotExistsClause | overwrite)?
	Ident
	(
		drop |
		(schemafull | schemaless) |
		TableTypeClause |
		TableViewClause |
		ChangefeedClause |
		PermissionsForClause |
		CommentClause
	)*
}

defineTokenOptions {
	(IfNotExistsClause | overwrite)?
	Ident
	( on ( namespace | database | scope Ident ) )
	TokenTypeClause
	( valueKeyword String )
}

defineUserOptions {
	(IfNotExistsClause | overwrite)?
	Ident
	OnRootNsDbClause
	( ( password | passhash ) String )
	( roles commaSep<Ident> )
}

// Alter statement

AlterStatement {
	alter
	(
	( table alterTableOptions )
	)
}

alterTableOptions {
	IfNotExistsClause?
	Ident
	(
		drop |
		(schemafull | schemaless) |
		PermissionsForClause |
		CommentClause
	)*
}

// Remove statement

RemoveStatement {
	remove
	(
	( namespace IfExistsClause? Ident ) |
	( database IfExistsClause? Ident ) |
	( user IfExistsClause? Ident on ( root | namespace | database ) ) |
	( token IfExistsClause? Ident on ( namespace | database | scope ) ) |
	( event IfExistsClause? Ident on table? Ident ) |
	( field IfExistsClause? Ident on table? Ident ) |
	( index IfExistsClause? Ident on table? Ident ) |
	( analyzer IfExistsClause? Ident ) |
	( functionKw IfExistsClause? customFunctionName ) |
	( param IfExistsClause? VariableName ) |
	( scope IfExistsClause? Ident ) |
	( table IfExistsClause? Ident )
	)
}

// Data statements

CreateStatement {
	create only?
	createTarget { Ident | VariableName | FunctionCall | RecordId }
	createContent { ContentClause | SetClause | UnsetClause | () }
	ReturnClause?
	TimeoutClause?
	ParallelClause?
}

SelectStatement {
	(select)
	(
		valueKeyword Predicate |
		commaSep<inclusivePredicate>
	)
	(from)
	(only)?
	(
		statement |
		(
			commaSep<value>
			selectClauses1 {
				WithClause?
				WhereClause?
				SplitClause?
			}
			selectClauses2 {
				GroupClause?
				OrderClause?
				LimitStartComboClause?
			}
			selectClauses3 {
				FetchClause?
				TimeoutClause?
				ParallelClause?
				TempfilesClause?
				ExplainClause?
			}
		)
	)
}

DeleteStatement {
	delete only?
	(
		statement |
		(
			commaSep<value>
			WhereClause?
			ReturnClause?
			TimeoutClause?
			ParallelClause?
		)
	)
}

InsertStatement {
	insert
	ignore?
	relation?
	(into Ident)?
	Ident
	(
		Object |
		( "[" commaSep<Object> "]" ) |
		(
			"(" commaSep<Ident> ")"
			values
			commaSep<(
				"(" commaSep<value> ")"
			)>
			on duplicate key update
			commaSep<FieldAssignment>
		)
	)
}

IfElseStatement {
	if
	(
		Legacy {
			value
			then
			( Block | SubQuery)
			(
				else if
				value
				then
				( Block | SubQuery)
			)*
			(
				else
				( Block | SubQuery)
			)?
			end
		} |
		Modern {
			value Block
			(
				else if
				value Block
			)*
			(
				else Block
			)?
		}
	)
}

relateSubject { Array | Ident | FunctionCall | VariableName | RecordId }
RelateStatement {
	relate only?
	relateSubject "->" relateSubject "->" relateSubject
	( ContentClause | SetClause )?
	ReturnClause?
	TimeoutClause?
	ParallelClause?
}

InfoForStatement {
	info for
	(
		root |
		ns | namespace |
		db | database |
		( sc Ident | scope Ident) |
		( tb Ident | table Ident)
	)
	structure?
}

UpdateStatement {
	update only?
	(
		statement |
		(
			commaSep<value>
			updateClause1 {
				ContentClause |
				MergeClause |
				PatchClause |
				SetClause |
				UnsetClause
			}?
			updateClause2 {
				WhereClause?
				ReturnClause?
				TimeoutClause?
				ParallelClause?
			}
		)
	)
}

UpsertStatement {
	upsert only?
	(
		statement |
		(
			commaSep<value>
			updateClause1 {
				ContentClause |
				MergeClause |
				PatchClause |
				SetClause |
				UnsetClause
			}?
			updateClause2 {
				WhereClause?
				ReturnClause?
				TimeoutClause?
				ParallelClause?
			}
		)
	)
}

// Control statements
BreakStatement { break }
ContinueStatement { continue }
ForStatement { for VariableName in ( Array | VariableName | SubQuery | Block ) Block }
ReturnStatement { return expression }
ThrowStatement { throw value }

// Live statements
KillStatement { kill String }
LiveSelectStatement {
	live select
	(
		diff |
		valueKeyword Predicate |
		commaSep<inclusivePredicate>
	)
	from commaSep<( Ident | RecordId )>
	WhereClause?
	FetchClause?
}

// Other statements
LetStatement {
	let VariableName (":" type)? "=" value
}

ShowStatement {
	show changes for table
	Ident
	( since String )?
	( limit Int )?
}

SleepStatement {
	sleep Duration
}

useNs { ( ns | namespace ) Ident }
useDb { ( db | database ) Ident }
UseStatement {
	use
	( useNs | useDb | ( useNs useDb ) )
}

OptionStatement {
option
Ident
( "=" ( true | false ) )?
}

RebuildStatement {
rebuild index
IfExistsClause?
Ident
OnTableClause
}

// Values

baseValue {
	String |
	prefixedString |
	number |
	true |
	false |
	null |
	none |
	Regex |
	VariableName |
	Ident[@dynamicPrecedence=-1] { rawident } |
	Array |
	RecordId |
	FunctionJs |
	FunctionCall |
	SubQuery |
	Block |
	Closure |
	Object |
	TypeCast |
	Duration |
	Point { "(" Decimal "," Decimal ")" }
}

Any { "?" }
AnyStar[@name=Any] { "*" }

value[@isGroup=Value] {
	baseValue |
	BinaryExpression |
	Path
}

// Binary expressions

BinaryExpression {
	value !binary Operator value
}

Path {
	baseValue pathElement+ |
	GraphPath pathElement*
}

pathElement {
	GraphPath |
	Subscript {
		"?"?
		"."
		(
			Ident |
			FunctionName { rawident !method } ArgumentList |
			AnyStar |
			Destructure
		)
	} |
	Filter { !filter "[" (WhereClause | value | WhereClause { "?" value } ) "]" }
}

GraphPath {
	("<-" | "->" | "<->")
	(Ident | Any | "(" commaSep<GraphPredicate> ")")
}

GraphPredicate {
	(value | Any) WhereClause? (as Ident)?
}

Destructure {
	"{" commaSep<((Ident ":")? Ident pathElement?)> "}"
}

// Objects

ObjectKey {
	KeyName { rawident } |
	String
}

ObjectProperty {
	ObjectKey ":" value
}

ObjectContent {
	commaSepTrailing<ObjectProperty>
}

Object {
	objectOpen ObjectContent? "}"
}

// Arrays

Array { "[" commaSep<value>? "]"}

// Record Ids

RecordTbIdent {
	rawident |
	tickIdent |
	bracketIdent
}

RecordIdIdent {
	numberident |
	tickIdent |
	bracketIdent
}

recordIdValue {
	RecordIdIdent |
	Int |
	Array |
	Object
}

RecordIdRange {
	( recordIdValue !range ">"? )?
	".."
	( !range "="? recordIdValue )?
}

RecordId {
	RecordTbIdent
	":"
	( recordIdValue | RecordIdRange )
}

// Durations

Duration {
	DurationPart+
}

// JS Functions

FunctionJs {
	FunctionName { function }
	ArgumentList
	JavaScriptBlock
}

@skip {} {
	JavaScriptBlock {
		"{" JavaScriptContent { javaScriptElement* } "}"
	}
	javaScriptElement {
		javaScriptContent | javaScriptBraceOpen javaScriptElement* javaScriptBraceClose
	}
}

// Function invocation

FunctionCall {
	( count ArgumentListCount ) |
	(
	   (FunctionName | customFunctionName | rand)
    	Version { "<" VersionNumber ">" }?
    	ArgumentList
	)
}

ArgumentList { "(" commaSep<value>? ")" }
ArgumentListCount[@name=ArgumentList] { "(" ( commaSep<value>? | statement ) ")" }

// Assignment

FieldAssignment {
	Ident
	assignmentOperator
	value
}

// Clauses

MergeClause {
	merge Object
}

PatchClause {
	patch Array
}

TableTypeClause {
	typeKeyword
	(
		any |
		normal |
		(
			relation
			( ( in | from ) piped<Ident> )?
			( ( out | to ) piped<Ident> )?
		)
	)
}

TableViewClause {
	as
	select
	commaSep<inclusivePredicate>
	from
	commaSep<value>
	WhereClause?
	GroupClause?
}

ChangefeedClause {
	changefeed
	Duration
}

SessionClause {
	session
	Duration
}

SignupClause {
	signup
	( SubQuery | Block )
}

SigninClause {
	signin
	( SubQuery | Block )
}

AuthenticateClause {
	authenticate
	( SubQuery | Block )
}

JwtClause {
	algorithm Ident key Ident | url String
}

OnTableClause {
	on table? Ident
}

OnRootNsDbClause {
	on ( root | namespace | database )
}

AccessTypeClause {
	typeKeyword (
		( jwt JwtClause ) |
		( record SignupClause? SigninClause? ( with jwt JwtClause? ( with issuer key Ident )? )? )
	)
}

FieldsColumnsClause {
	( fields | columns ) commaSep<Ident>
}

UniqueClause {
	unique
}

SearchAnalyzerClause {
	search analyzer
	Ident
    (Bm25Clause | DocIdsOrderClause | DocLenghtsOrderClause | PostingsOrderClause |
	 TermsOrderClause | DocIdsCacheClause | DocLenghtsCacheClause | PostingsCacheClause |
	 TermsCacheClause | highlights)*
}

Bm25Clause { bm25 ( "(" Float "," Float ")")? }
DocIdsCacheClause { doc_ids_cache Int }
DocIdsOrderClause { doc_ids_order Int }
DocLenghtsCacheClause { doc_lengths_cache Int }
DocLenghtsOrderClause { doc_lengths_order Int }
PostingsCacheClause { postings_cache Int }
PostingsOrderClause { postings_order Int }
TermsCacheClause { terms_cache Int }
TermsOrderClause { terms_order Int }

MtreeClause {
	mtree
	IndexDimensionClause
	(
	MtreeDistClause |
	IndexTypeClause |
	IndexCapacityClause |
	DocIdsOrderClause |
	DocIdsCacheClause |
	MtreeCacheClause
	)*
}

MtreeCacheClause { mtree_cache Int }
MtreeDistClause { dist ( cosine | euclidean | manhattan ) }

HnswClause {
hnsw
IndexDimensionClause
	(
	HnswDistClause |
	IndexTypeClause |
	IndexCapacityClause |
	IndexLmClause |
	IndexM0Clause |
	IndexMClause |
	IndexEfcClause |
	IndexExtendCandidatesClause |
	IndexKeepPrunedConnectionsClause
	)*
}

HnswDistClause { dist ( chebyshev | cosine | euclidean | hamming | jaccard | manhattan | ( minkowski number ) | pearson ) }

IndexDimensionClause { dimension Int }
IndexTypeClause { typeKeyword ( f32 | f64 | i16 | i32 | i64 ) }
IndexCapacityClause { capacity Int }
IndexLmClause { lm Float }
IndexM0Clause { m0 Int }
IndexMClause { m Int }
IndexEfcClause { efc Int }
IndexExtendCandidatesClause { extend_candidates }
IndexKeepPrunedConnectionsClause { keep_pruned_connections }

ContentClause {
	content Object
}

SetClause {
	set commaSep<FieldAssignment>
}

UnsetClause {
unset commaSep<FieldAssignment>
}

IfNotExistsClause {
	if not exists
}

IfExistsClause {
	if exists
}

ReturnClause {
	return
	( before | after | diff | commaSep<inclusivePredicate> )
}

TimeoutClause {
	timeout
	Duration
}

ParallelClause {
	parallel
}

TempfilesClause {
	tempfiles
}

FetchClause {
	fetch
	commaSep<Ident>
}

StartClause {
	start
	at?
	(Int | VariableName)
}

LimitClause {
	limit
	by?
	(Int | VariableName)
}

LimitStartComboClause {
	StartClause LimitClause? |
	LimitClause StartClause?
}

GroupClause {
	group
	by?
	commaSep<Ident>
}

OrderClause {
	order
	by?
	commaSep<Ident>
	( asc | desc )?
}

SplitClause {
	split
	on?
	Ident
}

WhereClause {
	where
	value
}

WithClause {
	with
	(
		noindex |
		index commaSep<Ident>
	)
}

ExplainClause {
	explain
	full?
}

TokenizersClause { tokenizers commaSep<AnalyzerTokenizers> }

FiltersClause {
	filters
	commaSep<AnalyzerFilters>
}

FunctionClause {
	functionKw
	customFunctionName
}

WhenThenClause {
	(
		when
		value
	)?
	then?
	commaSep<(SubQuery | Block)>
}

TypeClause {
	flexible?
	typeKeyword
	type
}

ReadonlyClause {
	readonly
}

DefaultClause {
	default
	value
}

ValueClause {
	valueKeyword
	value
}

AssertClause {
	assert
	value
}

PermissionsForClause {
	permissions
	(
		none | full |
		(
			for
			commaSep<( selectPermissions | createPermissions | updatePermissions | deletePermissions )>
			( WhereClause | none | full )
		)+
	)
}

PermissionsBasicClause {
	permissions
	none | full | WhereClause
}

CommentClause {
	comment
	String
}

DurationClause {
	duration ( for token Duration ) ","? ( for session Duration )
}

TokenTypeClause {
	typeKeyword
	(
		jwks |
		eddsa |
		es256 |
		es384 |
		es512 |
		ps256 |
		ps384 |
		ps512 |
		rs256 |
		rs384 |
		rs512
	)
}

// Support

AnalyzerTokenizers {
	blank |
	camel |
	class |
	punct
}

AnalyzerFilters {
	ascii |
	lowercase |
	uppercase |
	( edgengram "(" Int "," Int ")" ) |
	( ngram "(" Int "," Int ")" ) |
	( snowball "(" Ident ")" )
}

Predicate {
	value |
	value as Ident
}

inclusivePredicate {
  AnyStar | Predicate
}

// Types

literalType {
	String |
	ArrayType { "[" type "]" } |
	ObjectType { objectOpen ObjectTypeContent? "}" }
	// TODO support number and duration types
}

singleType {
    TypeName { rawident } |
    ParameterizedType { singleType "<" (type | Int) ">" } |
    LiteralType { literalType }
}

type {
    singleType |
    UnionType { type (!union "|" type)+ }
}

safeType {
  singleType | "<" type ">"
}

ObjectTypeContent {
	commaSep<ObjectTypeProperty>
}

ObjectTypeProperty {
	ObjectKey ":" type
}

TypeCast {
	"<" type ">" baseValue
}

Ident {
	rawident |
	tickIdent
}

number { Int | Float | Decimal }

Operator {
    binaryOperator |
	"-" | "=" | ">" | "<" |
	and |
	or |
	is |
	( is opNot ) |
	contains |
	containsnot |
	containsall |
	containsany |
	containsnone |
	inside | in |
	notinside | ( opNot opIn ) |
	allinside |
	anyinside |
	noneinside |

	outside |
	intersects |

	( "@" Int "@" ) |
	(
		"<|"
		Int
		( "," ( Int | chebyshev | cosine | euclidean | hamming | jaccard | manhattan | ( minkowski number ) | pearson ) )
		"|>"
	)
}

@external tokens objectToken from "./tokens" {
	objectOpen[@name=BraceOpen]
}

@tokens {
	// Characters
	"{"[@name=BraceOpen]
	"}"[@name=BraceClose]
	"[" "]"
	"<" ">"
	":"[@name=Colon]
	"->"[@name=ArrowRight]
	"<-"[@name=ArrowLeft]
	"<->"[@name=ArrowBoth]
	"|"[@name=Pipe]

	// Helpers
	whitespace { $[ \n\r\t] }

	// Comments
	Comment { CommentHash | CommentDash | CommentSlash }
	CommentHash { "#" ![\n]* }
	CommentDash { "--" ![\n]* }
	CommentSlash { "//" ![\n]* }
	CommentResult[@name=Comment,closedBy=CommentResultClose] { "-------- Query" ![\n]* }

	// Numbers
	Int { "-"? @digit+ }
	frac { '.' @digit+ }
	exp { $[eE] $[+\-]? @digit+ }
	Float { "-"? @digit+ (frac exp? | exp) "f"? }
	Decimal { "-"? @digit+ frac? exp? "dec" }
	VersionNumber { @digit+ ("." @digit+ ("." @digit+)?)? }

	String { "'" (!['\\] | "\\" _)* "'" | '"' (!["\\] | "\\" _)* '"' }
	prefixedString[@name=String] { $[ruds] String }

	// Language
	VariableName { "$" (rawident | tickIdent | bracketIdent) }
	tickIdent {
		"`" ![`]+ '`'
	}
	bracketIdent {
		"⟨" ![⟩]+ "⟩"
	}
	rawident { (@asciiLetter | "_") (@asciiLetter | @digit | "_")* }
	numberident {
		rawident |
		( @digit+ (@asciiLetter | "_") (@asciiLetter | @digit | "_")* )
	}

	functionNameSegment { (@asciiLetter | "_") (@asciiLetter | "_" | @digit)* }
	FunctionName {
		functionNameSegment ("::" functionNameSegment)+
	}
	customFunctionName[@name=FunctionName] {
		"fn" ("::" functionNameSegment)*
	}

	Regex { "/" (![/\\\n[] | "\\" ![\n] | "[" (![\n\\\]] | "\\" ![\n])* "]")+ ("/" $[dgimsuvy]*)? }

	durationUnit { "ns" | "us" | "µs" | "ms" | "s" | "m" | "h" | "d" | "w" | "y"}
	DurationPart { @digit+ " "* durationUnit }

	@precedence { CommentResult Comment Int "-" }
	@precedence { Comment binaryOperator }
	@precedence { prefixedString rawident }
	@precedence { customFunctionName FunctionName rawident }
	@precedence { Float Decimal Int rawident }
	@precedence { DurationPart rawident }
	@precedence { DurationPart Int }
	@precedence { numberident Int rawident }

	javaScriptContent { "\n" | ![{}\n'"]+ "\n"? | String }
	javaScriptBraceOpen { "{" }
	javaScriptBraceClose { "}" }

	binaryOperator {
		"&&" | "||" | "??" | "?:" | "!=" | "==" | "?=" | "*=" | "~" | "!~" | "*~" |
		"<=" | ">=" | "+" | "+=" | "-=" | "*" | "×" | "/"  | "÷" | "**" |
		$[∋∌⊇⊃⊅∈∉⊆⊂⊄] | "@@"
    }

	assignmentOperator[@name=Operator] {
		"="  |
		"+=" |
		"-="
	}
}

commaSep<content> {
	content ("," content)*
}

commaSepTrailing<content> {
	content ("," content)* ","?
}

piped<content> {
	content ("|" content)*
}

@skip { whitespace | Comment }
@detectDelim

@external propSource surqlHighlighting from "./highlight"

@external extend { rawident } tokens from "./tokens" {
	access					[@name=Keyword],
	algorithm				[@name=Keyword],
	alter					[@name=Keyword],
	analyzer				[@name=Keyword],
	any						[@name=Keyword],
	as						[@name=Keyword],
	asc						[@name=Keyword],
	assert					[@name=Keyword],
	at						[@name=Keyword],
	authenticate			[@name=Keyword],
	begin					[@name=Keyword],
	bm25					[@name=Keyword],
	break					[@name=Keyword],
	by						[@name=Keyword],
	cancel					[@name=Keyword],
	capacity				[@name=Keyword],
	changefeed				[@name=Keyword],
	changes					[@name=Keyword],
	columns					[@name=Keyword],
	comment					[@name=Keyword],
	commit					[@name=Keyword],
	content					[@name=Keyword],
	continue				[@name=Keyword],
	create					[@name=Keyword],
	database				[@name=Keyword],
	db						[@name=Keyword],
	default					[@name=Keyword],
	define					[@name=Keyword],
	delete					[@name=Keyword],
	desc					[@name=Keyword],
	dimension				[@name=Keyword],
	dist					[@name=Keyword],
	doc_ids_cache			[@name=Keyword],
	doc_ids_order			[@name=Keyword],
	doc_lengths_cache		[@name=Keyword],
	doc_lengths_order		[@name=Keyword],
	drop					[@name=Keyword],
	duplicate				[@name=Keyword],
	duration				[@name=Keyword],
	efc						[@name=Keyword],
	else					[@name=Keyword],
	end						[@name=Keyword],
	event					[@name=Keyword],
	exists					[@name=Keyword],
	explain					[@name=Keyword],
	extend_candidates		[@name=Keyword],
	fetch					[@name=Keyword],
	field					[@name=Keyword],
	fields					[@name=Keyword],
	filters					[@name=Keyword],
	flexible				[@name=Keyword],
	for						[@name=Keyword],
	from					[@name=Keyword],
	group					[@name=Keyword],
	highlights				[@name=Keyword],
	hnsw					[@name=Keyword],
	if						[@name=Keyword],
	ignore					[@name=Keyword],
	in						[@name=Keyword],
	index					[@name=Keyword],
	info					[@name=Keyword],
	insert					[@name=Keyword],
	into					[@name=Keyword],
	issuer					[@name=Keyword],
	jwt						[@name=Keyword],
	keep_pruned_connections	[@name=Keyword],
	key						[@name=Keyword],
	kill					[@name=Keyword],
	let						[@name=Keyword],
	limit					[@name=Keyword],
	live					[@name=Keyword],
	lm						[@name=Keyword],
	m						[@name=Keyword],
	m0						[@name=Keyword],
	merge					[@name=Keyword],
	mtree					[@name=Keyword],
	mtree_cache				[@name=Keyword],
	namespace				[@name=Keyword],
	noindex					[@name=Keyword],
	normal					[@name=Keyword],
	not						[@name=Keyword],
	ns						[@name=Keyword],
	on						[@name=Keyword],
	only					[@name=Keyword],
	option					[@name=Keyword],
	order					[@name=Keyword],
	out						[@name=Keyword],
	overwrite				[@name=Keyword],
	parallel				[@name=Keyword],
	param					[@name=Keyword],
	passhash				[@name=Keyword],
	password				[@name=Keyword],
	patch					[@name=Keyword],
	permissions				[@name=Keyword],
	postings_cache			[@name=Keyword],
	postings_order			[@name=Keyword],
	readonly				[@name=Keyword],
	rebuild					[@name=Keyword],
	record					[@name=Keyword],
	relate					[@name=Keyword],
	relation				[@name=Keyword],
	remove					[@name=Keyword],
	return					[@name=Keyword],
	roles					[@name=Keyword],
	root					[@name=Keyword],
	sc						[@name=Keyword],
	schemafull				[@name=Keyword],
	schemaless				[@name=Keyword],
	scope					[@name=Keyword],
	search					[@name=Keyword],
	select					[@name=Keyword],
	session					[@name=Keyword],
	set						[@name=Keyword],
	show					[@name=Keyword],
	signin					[@name=Keyword],
	signup					[@name=Keyword],
	since					[@name=Keyword],
	sleep					[@name=Keyword],
	split					[@name=Keyword],
	start					[@name=Keyword],
	structure				[@name=Keyword],
	table					[@name=Keyword],
	tb						[@name=Keyword],
	tempfiles				[@name=Keyword],
	terms_cache				[@name=Keyword],
	terms_order				[@name=Keyword],
	then					[@name=Keyword],
	throw					[@name=Keyword],
	timeout					[@name=Keyword],
	to						[@name=Keyword],
	token					[@name=Keyword],
	tokenizers				[@name=Keyword],
	transaction				[@name=Keyword],
	typeKeyword				[@name=Keyword],
	unique					[@name=Keyword],
	unset					[@name=Keyword],
	update					[@name=Keyword],
	upsert					[@name=Keyword],
	url						[@name=Keyword],
	use						[@name=Keyword],
	user					[@name=Keyword],
	valueKeyword			[@name=Keyword],
	values					[@name=Keyword],
	when					[@name=Keyword],
	where					[@name=Keyword],
	with					[@name=Keyword],

	// Literals
	after					[@name=Literal],
	before					[@name=Literal],
	diff					[@name=Literal],
	false					[@name=Bool],
	full					[@name=Literal],
	none					[@name=None],
	null					[@name=None],
	true					[@name=Bool],

	f32						[@name=Literal],
	f64						[@name=Literal],
	i16						[@name=Literal],
	i32						[@name=Literal],
	i64						[@name=Literal],

	createPermissions		[@name=PermissionTarget],
	deletePermissions		[@name=PermissionTarget],
	selectPermissions		[@name=PermissionTarget],
	updatePermissions		[@name=PermissionTarget],

	jwks					[@name=TokenType],
	eddsa					[@name=TokenType],
	es256					[@name=TokenType],
	es384					[@name=TokenType],
	es512					[@name=TokenType],
	ps256					[@name=TokenType],
	ps384					[@name=TokenType],
	ps512					[@name=TokenType],
	rs256					[@name=TokenType],
	rs384					[@name=TokenType],
	rs512					[@name=TokenType],

	// Operators
	allinside,
	and,
	anyinside,
	contains,
	containsall,
	containsany,
	containsnone,
	containsnot,
	inside,
	intersects,
	is,
	noneinside,
	notinside,
	opIn,
	opNot,
	or,
	outside,

	chebyshev 				[@name=Distance],
	cosine					[@name=Distance],
	euclidean				[@name=Distance],
	hamming					[@name=Distance],
	jaccard					[@name=Distance],
	manhattan				[@name=Distance],
	minkowski				[@name=Distance],
	pearson					[@name=Distance],

	ascii					[@name=Filter],
	edgengram				[@name=Filter],
	lowercase				[@name=Filter],
	ngram					[@name=Filter],
	snowball				[@name=Filter],
	uppercase				[@name=Filter],

	blank					[@name=Tokenizer],
	camel					[@name=Tokenizer],
	class					[@name=Tokenizer],
	punct					[@name=Tokenizer],

	function,
	rand					[@name=FunctionName],
	count					[@name=FunctionName]
}

anyKeyword {
	access |
	algorithm |
	alter |
	analyzer |
	any |
	as |
	asc |
	assert |
	at |
	authenticate |
	begin |
	bm25 |
	break |
	by |
	cancel |
	capacity |
	changefeed |
	changes |
	columns |
	comment |
	commit |
	content |
	continue |
	create |
	database |
	db |
	default |
	define |
	delete |
	desc |
	dimension |
	dist |
	doc_ids_cache |
	doc_ids_order |
	doc_lengths_cache |
	doc_lengths_order |
	drop |
	duplicate |
	duration |
	efc |
	else |
	end |
	event |
	exists |
	explain |
	extend_candidates |
	fetch |
	field |
	fields |
	filters |
	flexible |
	for |
	from |
	group |
	highlights |
	hnsw |
	if |
	ignore |
	in |
	index |
	info |
	insert |
	into |
	issuer |
	jwt |
	keep_pruned_connections |
	key |
	kill |
	let |
	limit |
	live |
	lm |
	m |
	m0 |
	merge |
	mtree |
	mtree_cache |
	namespace |
	noindex |
	normal |
	not |
	ns |
	on |
	only |
	option |
	order |
	out |
	overwrite |
	parallel |
	param |
	passhash |
	password |
	patch |
	permissions |
	postings_cache |
	postings_order |
	readonly |
	rebuild |
	record |
	relate |
	relation |
	remove |
	return |
	roles |
	root |
	sc |
	schemafull |
	schemaless |
	scope |
	search |
	select |
	session |
	set |
	show |
	signin |
	signup |
	since |
	sleep |
	split |
	start |
	structure |
	table |
	tb |
	tempfiles |
	terms_cache |
	terms_order |
	then |
	throw |
	timeout |
	to |
	token |
	tokenizers |
	transaction |
	typeKeyword |
	unique |
	unset |
	update |
	upsert |
	url |
	use |
	user |
	valueKeyword |
	values |
	when |
	where |
	with
}

anyLiteral {
	after |
	before |
	diff |
	false |
	full |
	none |
	null |
	true |

	f32 |
	f64 |
	i16 |
	i32 |
	i64
}

anyPermission {
	createPermissions |
	deletePermissions |
	selectPermissions |
	updatePermissions
}
